LIST >>> = >>> ;
LIST <<< = <<< ;
DELIMITERS = "<.>" ;

LIST A = "a" ;
LIST B = "b" ;
LIST C = "c" ;
LIST ANY = "a" "b" "c" ;
LIST OPTA = (opta) ;
LIST OPTB = (optb) ;
LIST OPTC = (optc) ;

LIST X1 = (x1) ;
LIST X2 = (x2) ;
LIST X3 = (x3) ;
SET X1_OR_X2 = X1 OR X2 ;
LIST FAIL = (fail) ;

LIST FST = (fst) ;
LIST SND = (snd) ;
LIST OPTFST = (optfst) ;
LIST OPTSND = (optsnd) ;

# ADD COUNT BY 3
SECTION
ADD X1 TARGET ANY (-1 >>>) ;

SECTION 
ADD X2 TARGET ANY (-1 X1) ;
ADD X3 TARGET ANY (-1 X2) ;
ADD X1 TARGET ANY (-1 X3) ;

SECTION
ADD FAIL TARGET X1_OR_X2 (1 <<<) ;

SECTION 
REMCOHORT ANY (1* FAIL) ;

SECTION
REMCOHORT ANY (0 FAIL) ;

# DIVIDE DOCUMENT IN FST/SND HALF
SECTION
ADD FST TARGET ANY (-1 >>>) ;
ADD SND TARGET ANY ( 1 <<<) ;

SECTION
ADD FST    TARGET ANY ( 0 OPTFST) ;
ADD SND    TARGET ANY ( 0 OPTSND) ;
ADD FST    TARGET ANY (-1 OPTFST LINK NOT 0 OPTSND) ;
ADD SND    TARGET ANY ( 1 OPTSND LINK NOT 0 OPTFST) ;
ADD OPTFST TARGET ANY (-1 FST    LINK NOT 0 SND) ;
ADD OPTSND TARGET ANY ( 1 SND    LINK NOT 0 FST) ;

# SELECT FIRST TWO, THE MIDDLE, THE LAST TWO
SECTION
SELECT A (-1 >>>) ;
SELECT B ( 0 FST LINK 0 SND) ;
SELECT C ( 1 <<<) ;
SELECT A (-2 >>>) ; # IF NOT AFTER, WE GET "AAC" 
SELECT C ( 2 <<<) ; # FOR LENGTH 3

SECTION
ADD OPTA TARGET A (-1C A) ;
ADD OPTC TARGET C ( 1C C) ;
ADD OPTB TARGET B (-1C B) ;
ADD OPTB TARGET B ( 1C B) ;
SELECT A (0 OPTA) ;
SELECT C (0 OPTC) ;
SELECT B (0 OPTB) ;
